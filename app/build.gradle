apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    lintOptions {
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "mobi.maptrek"
        minSdkVersion 23
        targetSdkVersion 25
        versionCode 53
        versionName "2017.12"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            //shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "tier", "region"

    productFlavors {
        full {
            dimension "tier"
            buildConfigField "boolean", "FULL_VERSION", "true"
        }
        lite {
            dimension "tier"
            applicationIdSuffix ".lite"
            versionNameSuffix "-lite"
            buildConfigField "boolean", "FULL_VERSION", "false"
        }
        world {
            dimension "region"
            buildConfigField "boolean", "RUSSIAN_EDITION", "false"
        }
        russia {
            dimension "region"
            applicationIdSuffix ".re"
            buildConfigField "boolean", "RUSSIAN_EDITION", "true"
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        // To check for a certain build type, use variant.buildType.name == "<buildType>"
        if (names.contains("full") && names.contains("russia")) {
            // Gradle ignores any variants that satisfy the conditions above.
            setIgnore(true)
        }
    }

    // we need applicationId to be resolved first
    applicationVariants.all { variant ->
        variant.buildConfigField "String", "EXPORT_PROVIDER_AUTHORITY", "\"${applicationId}.files\""
    }
}

repositories {
    mavenLocal()
    maven { url "https://jitpack.io" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.google.code.findbugs:annotations:2.0.1'
    compile 'com.android.support:support-annotations:27.0.2'
    compile 'com.android.support:design:25.4.0'
    compile 'org.mapsforge:vtm:master-SNAPSHOT'
    compile 'org.mapsforge:vtm-http:master-SNAPSHOT'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-arm64-v8a'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-armeabi'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-armeabi-v7a'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-mips'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-mips64'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-x86'
    compile 'org.mapsforge:vtm-android:master-SNAPSHOT:natives-x86_64'
    compile 'com.github.andreynovikov:androidcolorpicker:v0.0.2'
    compile 'com.github.andreynovikov:Geo-Coordinate-Conversion-Java:v1.0.0'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'com.noveogroup.android:android-logger:1.3.6'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.squareup.okhttp:okhttp:2.6.0'
    compile 'org.openstreetmap.osmosis:osmosis-osm-binary:0.45'
    compile 'com.fasterxml.jackson.core:jackson-core:2.8.4'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'com.github.jakob-grabner:Circle-Progress-View:v1.2.9'
    compile 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'
    compile 'com.github.apl-devs:appintro:v4.2.2'
    compile 'de.hdodenhof:circleimageview:2.2.0'
    compile 'com.github.andreynovikov:AndroidSeekBarPreference:master-SNAPSHOT'
}
